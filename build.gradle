import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
    	jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
	    classpath "com.blackducksoftware.integration:common-gradle-plugin:0.0.+"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: "io.spring.dependency-management"
apply plugin: 'maven'
apply plugin: 'maven-publish'

version =  '4.4.7'
def appName='blackduck-imageinspector'

apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

// TODO these don't need to be arrays:
def linuxFlavorsDpkg = ["ubuntu:latest"]
def linuxFlavorsRpm = ["centos:latest"]
def linuxFlavorsApk = ["alpine:latest"]
def linuxFlavors = linuxFlavorsDpkg + linuxFlavorsRpm + linuxFlavorsApk

String blackduckDir = "/opt/blackduck"
String imagePgmDir = "${blackduckDir}/${appName}"
String dockerInspectorDir = "${blackduckDir}/blackduck-docker-inspector"

final def versionFile = new File("${projectDir}/src/main/resources/version.properties")
versionFile.delete()
versionFile << "program.version=${version}"

publishing {
    publications {
        mavenJava(MavenPublication) {
            def artifactName = "${buildDir}/libs/${appName}-${version}.jar"
            artifact (artifactName)
        }
    }
}

artifactory {
    publish {
        defaults { publications ('mavenJava') }
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.blackducksoftware.integration:blackduck-common:42.3.0'
    implementation 'com.blackducksoftware.integration:hub-imageinspector-lib:9.0.5'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.apache.commons:commons-exec:1.3'
    testImplementation 'io.fabric8:kubernetes-client:3.1.8'
    testImplementation 'io.fabric8:kubernetes-model:2.0.8'
}

// Prepare test config file
// System.getProperty("user.dir")
task buildKubeTestPodConfig(type: Copy) {
	from 'src/test/resources/kube-test-pod.yml'
	into 'build/classes/java/test/com/synopsys/integration/blackduck/imageinspectorws/app'
	filter(ReplaceTokens, tokens: [CURRENT_DIR: System.getProperty("user.dir"), VERSION: version])
}

task testPrep(dependsOn: buildKubeTestPodConfig) {}

//////////// Create Docker Containers ///////////
task createApkDockerfileTasks() {
    linuxFlavorsApk.eachWithIndex {linuxFlavor, index ->
        String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
        task "create${linuxFlavorDirName}Dockerfile"(type: Dockerfile) {
            destFile = project.file("${buildDir}/images/${linuxFlavorDirName}/${appName}/Dockerfile")
            println "destFile: ${destFile}"
            from linuxFlavor
            maintainer 'Black Duck Software'
            exposePort 8080
            runCommand 'apk update && apk add bash && apk add openjdk8-jre'
            runCommand "mkdir -p ${imagePgmDir}/shared/target"
            runCommand "mkdir -p ${imagePgmDir}/shared/output"
            addFile("${appName}-${version}.jar", "${imagePgmDir}/${appName}.jar")
            runCommand "chown -R 10001 ${imagePgmDir}"
            runCommand "chmod -R g+w /lib/apk"
            runCommand "mkdir -p ${dockerInspectorDir}/config"
            runCommand "mkdir -p ${dockerInspectorDir}/target"
            runCommand "mkdir -p ${dockerInspectorDir}/output"
            runCommand "mkdir -p ${dockerInspectorDir}/working"
            runCommand "chmod -R g+w ${dockerInspectorDir}"
            user '10001'
            defaultCommand "java", "-jar", "${imagePgmDir}/${appName}.jar", "--server.port=8080", "--current.linux.distro=${linuxFlavorDirName}"
        }
    }
}

task createRpmDockerfileTasks() {
    linuxFlavorsRpm.eachWithIndex {linuxFlavor, index ->
        String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
        task "create${linuxFlavorDirName}Dockerfile"(type: Dockerfile) {
            destFile = project.file("${buildDir}/images/${linuxFlavorDirName}/${appName}/Dockerfile")
            println "destFile: ${destFile}"
            from linuxFlavor
            maintainer 'Black Duck Software'
            exposePort 8081
            runCommand 'yum update -y && yum install -y java-1.8.0-openjdk && yum clean all -y'
            runCommand "mkdir -p ${imagePgmDir}/shared/target"
            runCommand "mkdir -p ${imagePgmDir}/shared/output"
            addFile("${appName}-${version}.jar", "${imagePgmDir}/${appName}.jar")
            runCommand "chown -R 10001 ${imagePgmDir}"
            runCommand "chmod -R g+w /var/lib"
            runCommand "mkdir -p ${dockerInspectorDir}/config"
            runCommand "mkdir -p ${dockerInspectorDir}/target"
            runCommand "mkdir -p ${dockerInspectorDir}/output"
            runCommand "mkdir -p ${dockerInspectorDir}/working"
            runCommand "chmod -R g+w ${dockerInspectorDir}"
            runCommand "localedef -c -f UTF-8 -i en_US en_US.UTF-8"
            environmentVariable('LANG', 'en_US.UTF-8')
            user '10001'
            defaultCommand "java", "-jar", "${imagePgmDir}/${appName}.jar", "--server.port=8081", "--current.linux.distro=${linuxFlavorDirName}"
        }
    }
}

task createDpkgDockerfileTasks() {
    linuxFlavorsDpkg.eachWithIndex {linuxFlavor, index ->
        String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
        task "create${linuxFlavorDirName}Dockerfile"(type: Dockerfile) {
            destFile = project.file("${buildDir}/images/${linuxFlavorDirName}/${appName}/Dockerfile")
            println "destFile: ${destFile}"
            from linuxFlavor
            maintainer 'Black Duck Software'
            exposePort 8082
            runCommand 'apt-get update -y && \
                apt-get install -y openjdk-8-jdk && \
                apt-get install -y locales && apt-get -y clean'
            runCommand "mkdir -p ${imagePgmDir}/shared/target"
            runCommand "mkdir -p ${imagePgmDir}/shared/output"
            addFile("${appName}-${version}.jar", "${imagePgmDir}/${appName}.jar")
            runCommand "chown -R 10001 ${imagePgmDir}"
            runCommand "chmod -R g+w /var/lib/dpkg"
            runCommand "mkdir -p ${dockerInspectorDir}/config"
            runCommand "mkdir -p ${dockerInspectorDir}/target"
            runCommand "mkdir -p ${dockerInspectorDir}/output"
            runCommand "mkdir -p ${dockerInspectorDir}/working"
            runCommand "chmod -R g+w ${dockerInspectorDir}"
            runCommand 'locale-gen "en_US.UTF-8"'
            environmentVariable('LANG', 'en_US.UTF-8')
            user '10001'
            defaultCommand "java", "-jar", "${imagePgmDir}/${appName}.jar", "--server.port=8082", "--current.linux.distro=${linuxFlavorDirName}"
        }
    }
}

task createDockerfiles(dependsOn: [createubuntuDockerfile, createcentosDockerfile, createalpineDockerfile]) {
}

task createCopyJarToDockerfileDirTasks(type: Copy, dependsOn: [createDockerfiles]) {
	linuxFlavors.eachWithIndex {linuxFlavor, index ->
		String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
		println "Creating task to copy jar from build/libs/${project.name}-${version}.jar to Dockerfile dir for ${linuxFlavorDirName}, renaming to ${appName}-${version}.jar"
        task "copyJarToDockerfileDir_${linuxFlavorDirName}"(type: Copy, dependsOn: build) {
			from "build/libs/${project.name}-${version}.jar"
			into "${buildDir}/images/${linuxFlavorDirName}/${appName}"
			rename ("${project.name}-${version}.jar", "${appName}-${version}.jar")
		}
	}
}

task copyJarFileToDockerfileDirs(dependsOn: [copyJarToDockerfileDir_ubuntu, copyJarToDockerfileDir_centos, copyJarToDockerfileDir_alpine]) {
}

task createRemoveDockerImageTasks() {
    linuxFlavors.eachWithIndex {linuxFlavor, index ->
        String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
        def taskName = "removeDockerImage_${linuxFlavorDirName}"
        String suffix = getSuffix(linuxFlavorDirName)
        println "creating task ${taskName}"
        task "${taskName}"(type: Exec) {
			ignoreExitValue true
            commandLine "docker", "rmi", "blackducksoftware/${appName}${suffix}:${version}"
        }
    }
}

task buildImageDirs(dependsOn: [createDockerfiles, copyJarFileToDockerfileDirs]) {
}

task createBuildDockerImageTasks() {
    linuxFlavors.eachWithIndex {linuxFlavor, index ->
        String linuxFlavorDirName = getNameWithoutVersion(linuxFlavor)
        def taskName = "buildDockerImage_${linuxFlavorDirName}"
        String suffix = getSuffix(linuxFlavorDirName)
        println "creating task ${taskName}"
        task "${taskName}"(type: Exec, dependsOn: ["removeDockerImage_${linuxFlavorDirName}", buildImageDirs]) {
            commandLine "docker", "build", "--tag", "blackducksoftware/${appName}${suffix}:${version}", \
                "${buildDir}/images/${linuxFlavorDirName}/${appName}"
        }
    }
}



task buildImages(dependsOn: [buildDockerImage_alpine, buildDockerImage_centos, buildDockerImage_ubuntu]) {
}

testIntegration.dependsOn testPrep
testIntegration.dependsOn buildImages

task dockerLogin(type: Exec) {
    commandLine "docker", "login", "--username", "$System.env.DOCKER_HUB_USER", "--password", "$System.env.DOCKER_HUB_PASSWORD"
}

task pushImageAlpine(type: Exec, dependsOn: [buildDockerImage_alpine, dockerLogin]) {
	commandLine "docker", "push", "blackducksoftware/${appName}-alpine:${version}"
}

task pushImageCentos(type: Exec, dependsOn: [buildDockerImage_centos, dockerLogin]) {
	commandLine "docker", "push", "blackducksoftware/${appName}-centos:${version}"
}

task pushImageUbuntu(type: Exec, dependsOn: [buildDockerImage_ubuntu, dockerLogin]) {
	commandLine "docker", "push", "blackducksoftware/${appName}-ubuntu:${version}"
}

task pushImages(dependsOn: [pushImageAlpine, pushImageCentos, pushImageUbuntu]) {
}


//// helper methods ////
String getNameWithoutVersion(String linuxFlavor) {
    String linuxFlavorDirName = linuxFlavor
    int colonIndex = linuxFlavor.indexOf(':')
    if (colonIndex >= 0) {
        linuxFlavorDirName = linuxFlavor.subSequence(0, colonIndex)
    }
    linuxFlavorDirName
}

private String getSuffix(String linuxFlavorName) {
    String suffix = "-${linuxFlavorName}"
    suffix
}
